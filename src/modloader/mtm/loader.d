/*
 *             Copyright Andrej Mitrovic 2014.
 *  Distributed under the Boost Software License, Version 1.0.
 *     (See accompanying file LICENSE_1_0.txt or copy at
 *           http://www.boost.org/LICENSE_1_0.txt)
 */
module modloader.mtm.loader;

import std.algorithm : filter;
import std.conv      : text;
import std.exception : enforce;

import modloader.util : modDir, streamFile, ZCharArray;

import modloader.mtm.internal :
    ModuleInternal,   toModule,
    TrackRowInternal, toTrackRow,
    SampleInternal,   toSample;

import modloader.mtm.types : Module, Sample, Track, TrackRow, Pattern;
import modloader.mtm.util  : decodeComment, toSampleSize;

/**
    Read a Multi Track Module file.

    The MTM format is generated by the Multi Track Module tracker by the demo group
    Renaissance. The tracker features up to 32 channel digital music. Instead of
    saving whole patterns, the tracker only saves the different tracks and the data
    which tracks should be played together at which time, thus saving some pattern
    space.

    MTM Reference:
    ftp://ftp.modland.com/pub/documents/format_documentation/MultiTracker%20%28.mtm%29%20%233.txt
*/
Module readMTM(string path)
{
    import std.array : uninitializedArray;

    auto file = path.streamFile;
    enforce(file.size > 0);

    auto modInternal = file.read!ModuleInternal;

    with (modInternal)
    {
        enforce(id == "MTM");
        enforce(version_ == 16);  // note: unspecified
        enforce(lastPattern > 0);
        enforce(lastOrder > 0);
        enforce(numTracks > 0);
        enforce(numSamples > 0);
        enforce(numChannels > 0 && numChannels <= 32, numChannels.text);
    }

    auto mod = modInternal.toModule();

    enum HeaderSize = 66;
    enforce(file.pos == HeaderSize);

    enum SampleByteSize = 37;
    enum PatternOrderSize = 128;
    enum TrackByteSize = 64 * 3;
    enum PatternByteSize = 32 * short.sizeof;

    const totalBytes = HeaderSize +
                       SampleByteSize * modInternal.numSamples +
                       PatternOrderSize +
                       TrackByteSize * modInternal.numTracks +
                       PatternByteSize * modInternal.numOfPatterns +
                       modInternal.commentSize;

    enforce(totalBytes < file.size);

    mod.samples = uninitializedArray!(Sample[])(modInternal.numSamples);

    foreach (ref sample; mod.samples)
    {
        sample = file.read!SampleInternal.toSample;
    }

    enforce(file.pos == 0x42 + modInternal.numSamples * SampleByteSize);

    mod.sequence = file.read!(ubyte[128])[0 .. modInternal.numOfOrders].dup;

    enforce(file.pos == 0xC2 + modInternal.numSamples * SampleByteSize);

    mod.tracks = uninitializedArray!(Track[])(modInternal.numTracks);

    foreach (ref track; mod.tracks)
    {
        foreach (ref row; track.rows)
        {
            row = file.read!TrackRowInternal.toTrackRow;
        }
    }

    enforce(file.pos == 0xC2 +
            modInternal.numSamples * SampleByteSize +
            modInternal.numTracks * TrackByteSize);

    mod.patterns = uninitializedArray!(Pattern[])(modInternal.numOfPatterns);
    foreach (ref pattern; mod.patterns)
    {
        pattern = file.read!Pattern;
    }

    enforce(file.pos == 0xC2 +
            modInternal.numSamples * SampleByteSize +
            modInternal.numTracks * TrackByteSize +
            modInternal.numOfPatterns * PatternByteSize);

    mod.comment = file.read!(char[])(modInternal.commentSize).decodeComment().idup;

    enforce(file.pos == 0xC2 +
            modInternal.numSamples * SampleByteSize +
            modInternal.numTracks * TrackByteSize +
            modInternal.numOfPatterns * PatternByteSize +
            modInternal.commentSize);

    foreach (ref sample; mod.samples.filter!(a => a.length))
    {
        size_t byteCount = sample.length * sample.type.toSampleSize;
        enforce(byteCount, sample.text);

        sample.data = uninitializedArray!(ubyte[])(byteCount);
        file.readTo(sample.data);
    }

    enforce(file.end);

    return mod;
}

///
version (TestModloader)
unittest
{
    import std.path : buildPath;
    import std.stdio : writeln;

    import modloader.mtm.util : prettyPrint;

    foreach (modFile; testModFiles)
    {
        auto mod = modDir.buildPath(modFile).readMTM;
        //~ mod.prettyPrint();
    }
}

version (unittest)
private immutable testModFiles =
[
    "hacksaw.mtm",
    //~ "modern_society.mtm",
];
